! $Id: ESMF_LogErr.F90,v 1.13.2.8 2006/11/22 01:00:21 theurich Exp $
!
! Earth System Modeling Framework
! Copyright 2002-2006, University Corporation for Atmospheric Research,
! Massachusetts Institute of Technology, Geophysical Fluid Dynamics
! Laboratory, University of Michigan, National Centers for Environmental
! Prediction, Los Alamos National Laboratory, Argonne National Laboratory,
! NASA Goddard Space Flight Center.
! Licensed under the University of Illinois-NCSA License.
!
!==============================================================================
!
!     ESMF LogErr Module
      module ESMF_LogErrMod
!
!==============================================================================
!
! This file contains the LogErr class definition and all LogErr class
! methods.
!
!------------------------------------------------------------------------------
! INCLUDES
!! this should be including ESMF.h, but since it also includes the cover
!! log macros, it can't be included here.  so just include each file 
!! individually.  If we add files to ESMF.h they also need to be added here.
#include "ESMF_Macros.inc"
#include "ESMF_Conf.inc"
#include "ESMF_Version.inc"
#include "ESMF_LogConstants.inc"
#include "ESMF_ErrReturnCodes.inc"


!BOPI
!============================================================================
! !MODULE: Fortran Interface to Log class. 
!
! !DESCRIPTION:
!
! The Fortran interface to the {\tt ESMF\_Log} class is written in both
! Fortran and C/C++.
! This file contains the interface code written in Fortran.  It also contains
! some utility functions used by the {\tt ESMF\_Log} class.
!
!------------------------------------------------------------------------------
! !USES:
    ! inherit from ESMF base class
    use ESMF_UtilTypesMod

implicit none

!
!------------------------------------------------------------------------------
! !PRIVATE TYPES:
!------------------------------------------------------------------------------

!     ! ESMF_MsgType
type ESMF_MsgType
    sequence
    integer      :: mtype
end type

!     ! Msg Types
type(ESMF_MsgType), parameter           :: &
    ESMF_LOG_INFO  =   ESMF_MsgType(1), &
    ESMF_LOG_WARNING = ESMF_MsgType(2), &
    ESMF_LOG_ERROR =   ESMF_MsgType(3)

!     ! ESMF_Halt
type ESMF_HaltType
    sequence
    integer      :: htype
end type

!     ! Halt Types
type(ESMF_HaltType), parameter           :: &
    ESMF_LOG_HALTNEVER  =  ESMF_HaltType(1), &
    ESMF_LOG_HALTWARNING = ESMF_HaltType(2), &
    ESMF_LOG_HALTERROR =   ESMF_HaltType(3)
    
!     ! ESMF_LogType
type ESMF_LogType
    sequence
    integer      :: ftype
end type
    
!     ! Log Types
type(ESMF_LogType), parameter		:: &
    ESMF_LOG_SINGLE = ESMF_LogType(1), &
    ESMF_LOG_MULTI = ESMF_LogType(2),  &
    ESMF_LOG_NONE = ESMF_LogType(3)
    
!     ! Log Entry                            
type ESMF_LOGENTRY
    private
    sequence  					
    integer		::  h,m,s,ms
    integer		::  line
    logical             ::  methodflag,lineflag,fileflag
    character(len=2*ESMF_MAXSTR) ::  msg
    character(len=32) 	::  file,method
    character(len=8) 	::  d
    character(len=8)	::  lt  			
end type ESMF_LOGENTRY

type ESMF_Log
    private
    sequence        
     
    integer                                         ::  maxElements
    integer                                         ::  stream 
    integer                              	    ::  fIndex
    integer                                         ::  unitNumber
    integer                                         ::  petNumber	
    logical					    ::  stopprogram
    logical					    ::  pad ! memory alignment
    type(ESMF_Logical)                              ::  flushImmediately    
    type(ESMF_Logical)                              ::  rootOnly    
    type(ESMF_Logical)                              ::  verbose  
    type(ESMF_Logical)			            ::  flushed 
    type(ESMF_Logical)			            ::  dirty
    type(ESMF_HaltType)                             ::  halt
    type(ESMF_LogType)			            ::  logtype      
#ifndef ESMF_NO_INITIALIZERS
    type(ESMF_LOGENTRY), dimension(:),pointer       ::  LOG_ENTRY=>Null()
    type(ESMF_Logical)                              ::  FileIsOpen=ESMF_False
#else
    type(ESMF_LOGENTRY), dimension(:),pointer       ::  LOG_ENTRY
    type(ESMF_Logical)                              ::  FileIsOpen
#endif                                          
    character(len=32)                               ::  nameLogErrFile
    character(len=ESMF_MAXSTR)                      ::  petNumLabel
    
end type ESMF_Log

!------------------------------------------------------------------------------
! !PUBLIC TYPES:
    public ESMF_LOG_INFO
    public ESMF_LOG_WARNING
    public ESMF_LOG_ERROR
    public ESMF_LOG_SINGLE
    public ESMF_LOG_MULTI
    public ESMF_LOG_NONE    
    public ESMF_LOG_HALTNEVER
    public ESMF_LOG_HALTWARNING
    public ESMF_LOG_HALTERROR
!------------------------------------------------------------------------------
!
! !PUBLIC MEMBER FUNCTIONS:
   public ESMF_Log
   public ESMF_LogClose
   public ESMF_LogFinalize
   public ESMF_LogFlush
   public ESMF_LogFoundAllocError
   public ESMF_LogFoundError
   public ESMF_LogGet
   public ESMF_LogInitialize
   public ESMF_LogMsgFoundAllocError
   public ESMF_LogMsgFoundError
   public ESMF_LogMsgSetError
   public ESMF_LogOpen
   public ESMF_LogSet
   public ESMF_LogWrite
   public ESMF_HaltType
   public ESMF_MsgType

!  Overloaded = operator functions
   public operator(.eq.),operator(.gt.)
   
! overload .eq. and .gt. with additional derived types so you can compare 
!  them as if they were simple integers.
 

interface operator (.eq.)
   module procedure ESMF_lmteq
   module procedure ESMF_lhteq
   module procedure ESMF_llteq
end interface

interface operator (.ne.)
   module procedure ESMF_lltne
end interface

interface operator (.gt.)
   module procedure ESMF_lmtgt
end interface
!EOPI  

type(ESMF_Log),SAVE,target::ESMF_LogDefault	
!----------------------------------------------------------------------------

contains

!------------------------------------------------------------------------------
! functions to compare two ESMF_DataTypes to see if they're the same or not

function ESMF_lhteq(ht1, ht2)
logical ESMF_lhteq
type(ESMF_HaltType), intent(in) :: ht1,ht2
    
    ESMF_lhteq = (ht1%htype .eq. ht2%htype)
end function

function ESMF_lmteq(mt1, mt2)
logical ESMF_lmteq
type(ESMF_MsgType), intent(in) :: mt1,mt2

    ESMF_lmteq = (mt1%mtype .eq. mt2%mtype)
end function

function ESMF_llteq(lt1, lt2)
logical ESMF_llteq
type(ESMF_LogType), intent(in) :: lt1,lt2

    ESMF_llteq = (lt1%ftype .eq. lt2%ftype)
end function

function ESMF_lltne(lt1, lt2)
logical ESMF_lltne
type(ESMF_LogType), intent(in) :: lt1,lt2

    ESMF_lltne = (lt1%ftype .ne. lt2%ftype)
end function

function ESMF_lmtgt(mt1, mt2)
logical ESMF_lmtgt
type(ESMF_MsgType), intent(in) :: mt1,mt2

    ESMF_lmtgt = (mt1%mtype .gt. mt2%mtype)
end function

!--------------------------------------------------------------------------
!BOP
! !IROUTINE: ESMF_LogClose - Close Log file(s)

! !INTERFACE: 
    subroutine ESMF_LogClose(log, rc)
!
! !ARGUMENTS:
    type(ESMF_Log)	                                        :: log
    integer, intent(out),optional                               :: rc

! !DESCRIPTION:
!      This routine closes the file(s) associated with the {\tt log}.
!
!      The arguments are:
!      \begin{description}
! 
!      \item [log]
!            An {\tt ESMF\_Log} object.
!      \item [{[rc]}]
!            Return code; equals {\tt ESMF\_SUCCESS} if there are no errors.
!      \end{description}
! 
!EOP
	
	integer::rc2,status
	
	if (present(rc)) then
            rc=ESMF_FAILURE
        endif

    if (log%logtype .ne. ESMF_LOG_NONE) then


	if (log%FileIsOpen .eq. ESMF_TRUE) then
	    call ESMF_LogFlush(log,rc=rc2)		
    	    CLOSE(UNIT=log%unitNumber)
	    log%FileIsOpen=ESMF_FALSE
	    deallocate(log%LOG_ENTRY,stat=status)	
	endif
    endif

	if (present(rc)) then
            rc=ESMF_SUCCESS
        endif
	
end subroutine ESMF_LogClose

!--------------------------------------------------------------------------
!BOPI
! !IROUTINE: ESMF_LogFinalize - Finalize Log file(s)

! !INTERFACE: 
	subroutine ESMF_LogFinalize(rc)
!
! !ARGUMENTS:
    integer, intent(out),optional	                        :: rc

! !DESCRIPTION:
!      This routine finalizes the global Log.  The default Log will be flushed
!      and the default Log will be closed.
!
!      The arguments are:
!      \begin{description}
! 
!      \item [{[rc]}]
!            Return code; equals {\tt ESMF\_SUCCESS} if there are no errors.
!      \end{description}
! 
!EOPI

        call ESMF_LogClose(ESMF_LogDefault, rc)

end subroutine ESMF_LogFinalize

!--------------------------------------------------------------------------
!BOP
! !IROUTINE: ESMF_LogFlush - Flushes the Log file(s)

! !INTERFACE: 
	subroutine ESMF_LogFlush(log,rc)
!
!
! !ARGUMENTS:
        type(ESMF_LOG), target,optional				:: log
	integer, intent(out),optional		                :: rc

! !DESCRIPTION:
!      This subroutine flushes the {\tt ESMF\_Log} buffer to its
!      associated file.
!
!      The arguments are:
!      \begin{description}
!
!      \item [{[log]}]
!            An optional {\tt ESMF\_Log} object that can be used instead
!            of the default Log.
!      \item [{[rc]}]
!            Return code; equals {\tt ESMF\_SUCCESS} if there are no errors.
!      \end{description}
! 
!EOP
    integer 			    :: j
    type(ESMF_LOG),pointer          :: alog

    if (present(log)) then
      alog => log
    else
      alog => ESMF_LogDefault
    endif
    
    if (present(rc)) then
      rc=ESMF_FAILURE 
    endif

    if (alog%FileIsOpen .ne. ESMF_TRUE) then
      print *, "ESMF_Log not open -- cannot ESMF_LogFlush()."
      return
    endif
      
    if ((alog%FileIsOpen .eq. ESMF_TRUE) .AND. &
        (alog%flushed .eq. ESMF_FALSE) .AND. &
	(alog%dirty .eq. ESMF_TRUE))  then	
	    do j=1, alog%fIndex-1
    	        if (alog%LOG_ENTRY(j)%lineflag) then
    	            if (alog%LOG_ENTRY(j)%methodflag) then
    		        WRITE(alog%unitNumber,122) &
                              alog%LOG_ENTRY(j)%d, " ", &
                              alog%LOG_ENTRY(j)%h, &
                              alog%LOG_ENTRY(j)%m, &
                              alog%LOG_ENTRY(j)%s, ".", &
                              alog%LOG_ENTRY(j)%ms, " ", &
                              alog%LOG_ENTRY(j)%lt, " ", &
			      trim(alog%petNumLabel), " ", &
                              trim(alog%LOG_ENTRY(j)%file) , " ", &
                              alog%LOG_ENTRY(j)%line, " ", &
                              trim(alog%LOG_ENTRY(j)%method), " ", &
                              trim(alog%LOG_ENTRY(j)%msg)
    		    else
    		        WRITE(alog%unitNumber,123) &
                              alog%LOG_ENTRY(j)%d, " ", &
                              alog%LOG_ENTRY(j)%h, &
                              alog%LOG_ENTRY(j)%m, &
                              alog%LOG_ENTRY(j)%s, ".", &
                              alog%LOG_ENTRY(j)%ms, " ", &
                              alog%LOG_ENTRY(j)%lt, " ", &
			      trim(alog%petNumLabel), " ", &
                              trim(alog%LOG_ENTRY(j)%file), " ", &
                              alog%LOG_ENTRY(j)%line, " ", &
                              trim(alog%LOG_ENTRY(j)%msg)
    		    endif
                else
    		    if (alog%LOG_ENTRY(j)%methodflag) then
    		        WRITE(alog%unitNumber,132) &
                              alog%LOG_ENTRY(j)%d, " ", &
                              alog%LOG_ENTRY(j)%h, &
                              alog%LOG_ENTRY(j)%m, &
                              alog%LOG_ENTRY(j)%s, ".", &
                              alog%LOG_ENTRY(j)%ms, " ", &
                              alog%LOG_ENTRY(j)%lt, " ", &
		              trim(alog%petNumLabel), " ", &
    			      trim(alog%LOG_ENTRY(j)%method), " ", &
                              trim(alog%LOG_ENTRY(j)%msg)
    		    else
    		        WRITE(alog%unitNumber,133) &
                              alog%LOG_ENTRY(j)%d, " ", &
                              alog%LOG_ENTRY(j)%h, &
                              alog%LOG_ENTRY(j)%m, &
                              alog%LOG_ENTRY(j)%s , ".", &
                              alog%LOG_ENTRY(j)%ms , " ", &
                              alog%LOG_ENTRY(j)%lt, " ", &
			      trim(alog%petNumLabel), " ", &
                              trim(alog%LOG_ENTRY(j)%msg)
    		    endif
    	        endif
	    enddo
   endif
   
   alog%fIndex = 1 
   122  FORMAT(a8,a,3i2.2,a,i6.6,7a,i0,4a)
   123  FORMAT(a8,a,3i2.2,a,i6.6,7a,i0,2a)
   132  FORMAT(a8,a,3i2.2,a,i6.6,8a)
   133  FORMAT(a8,a,3i2.2,a,i6.6,6a)
   
   alog%flushed = ESMF_TRUE
   alog%dirty = ESMF_FALSE
   rc=ESMF_SUCCESS	
      
end subroutine ESMF_LogFlush
!--------------------------------------------------------------------------
!BOP
! !IROUTINE:  ESMF_LogFoundAllocError - Check Fortran status for allocation error

! !INTERFACE: 
	function ESMF_LogFoundAllocError(statusToCheck, line, file, & 
                                         method, rcToReturn,log)
!
! !RETURN VALUE:
	logical                                     ::ESMF_LogFoundAllocError
! !ARGUMENTS:
!	
	integer, intent(in)                         :: statusToCheck
	integer, intent(in), optional               :: line
	character(len=*), intent(in), optional      :: file
	character(len=*), intent(in), optional      :: method
	integer, intent(out),optional               :: rcToReturn
	type(ESMF_LOG),intent(in),optional	    :: log

! !DESCRIPTION:
!      This function returns a logical true when a Fortran status code
!      returned from a memory allocation indicates an allocation error.  
!      An ESMF predefined memory allocation error 
!      message will be added to the {\tt ESMF\_Log} along with {\tt line}, 
!      {\tt file} and {\tt method}.  Additionally, the 
!      {\tt statusToCheck} will be converted to a {\tt rcToReturn}.
!
!      The arguments are:
!      \begin{description}
! 	
!      \item [statusToCheck]
!            Fortran allocation status to check.
!      \item [{[line]}]
!            Integer source line number.  Expected to be set by
!            using the preprocessor macro {\tt \_\_LINE\_\_} macro.
!      \item [{[file]}]
!            User-provided source file name. 
!      \item [{[method]}]
!            User-provided method string.
!      \item [{[rcToReturn]}]
!            If specified, set the {\tt rcToReturn} value to 
!            {\tt ESMF\_RC\_MEM} which is the error code for a memory 
!            allocation eror.
!      \item [{[log]}]
!            An optional {\tt ESMF\_Log} object that can be used instead
!	     of the default Log.
!      
!      \end{description}
! 
!EOP
    character(len=ESMF_MAXSTR)::tempmsg
    character(len=ESMF_MAXSTR)::allocmsg
	integer::msglen=0
	
    ESMF_LogFoundAllocError=.FALSE.
    if (statusToCheck .NE. 0) then
        if (present(rcToReturn)) then
            rcToReturn=ESMF_RC_MEM
        endif
        call c_esmc_loggeterrormsg(ESMF_RC_MEM,tempmsg,msglen)
        allocmsg=tempmsg(1:msglen)
	call ESMF_LogWrite(trim(allocmsg),ESMF_LOG_ERROR,line,file,method,log)
	ESMF_LogFoundAllocError=.TRUE.
    else
        if (present(rcToReturn)) then
            rcToReturn=ESMF_SUCCESS
        endif
    endif	
       
end function ESMF_LogFoundAllocError

!--------------------------------------------------------------------------
!BOP
! !IROUTINE: ESMF_LogFoundError - Check ESMF return code for error

! !INTERFACE: 
	function ESMF_LogFoundError(rcToCheck, line, file, method,& 
	         rcToReturn, log)
!
! !RETURN VALUE:
	logical                                         ::ESMF_LogFoundError
! !ARGUMENTS:
!	
	integer, intent(in)                             :: rcToCheck
	integer, intent(in), optional                   :: line
	character(len=*), intent(in), optional          :: file
	character(len=*), intent(in), optional	        :: method
	integer, intent(out), optional                  :: rcToReturn
	type(ESMF_LOG),intent(in),optional		:: log
	
! !DESCRIPTION:
!      This function returns a logical true for ESMF return codes that indicate 
!      an error.  A predefined error message will added to the {\tt ESMF\_Log} 
!      along with {\tt line}, {\tt file} and {\tt method}.  Additionally, 
!      {\tt rcToReturn} will be set to {\tt rcToCheck}.
!
!      The arguments are:
!      \begin{description}
! 	
!      \item [rcToCheck]
!            Return code to check.
!      \item [{[line]}]
!            Integer source line number.  Expected to be set by
!            using the preprocessor macro {\tt \_\_LINE\_\_} macro.
!      \item [{[file]}]
!            User-provided source file name. 
!      \item [{[method]}]
!            User-provided method string.
!      \item [{[rcToReturn]}]
!            If specified, copy the {\tt rcToCheck} value to {\tt rc}.
!            This is not the return code for this function; it allows
!            the calling code to do an assignment of the error code
!            at the same time it is testing the value.
!      \item [{[log]}]
!            An optional {\tt ESMF\_Log} object that can be used instead
!	     of the default Log.
!      
!      \end{description}
! 
!EOP
	
    ESMF_LogFoundError = .FALSE.
    if (present(rcToReturn)) then
        rcToReturn = rcToCheck
    endif
    if (rcToCheck .NE. ESMF_SUCCESS) then
        call ESMF_LogWrite("StandardError",ESMF_LOG_ERROR,line,file,method,&
	log)
        ESMF_LogFoundError = .TRUE.
    endif	
       
end function ESMF_LogFoundError

!--------------------------------------------------------------------------
!BOPI
! !IROUTINE: ESMF_LogGet - Return information about a log object

! !INTERFACE: 
	subroutine ESMF_LogGet(log,verbose,flush,rootOnly,halt,logtype,stream,&
	                       maxElements,rc)
!
! !ARGUMENTS:
!	
        type(ESMF_LOG), target,optional			        :: log
	type(ESMF_Logical), intent(out),optional		:: verbose
	type(ESMF_Logical), intent(out),optional		:: flush
	type(ESMF_Logical), intent(out),optional		:: rootOnly
	type(ESMF_HaltType), intent(out),optional               :: halt
	type(ESMF_LogType), intent(out),optional	        :: logtype
	integer, intent(out),optional			        :: stream  
	integer, intent(out),optional			        :: maxElements
	integer, intent(out),optional			        :: rc
	

! !DESCRIPTION:
!      This subroutine returns properties about a Log object.
!
!      The arguments are:
!      \begin{description}
!
!      \item [{[log]}]
!            An optional {\tt ESMF\_Log} object that can be used instead
!            of the default Log.
!      \item [{[verbose]}]
!            Verbose flag.
!      \item [{[flush]}]
!            Flush flag.
!      \item [{[rootOnly]}]
!	     Root only flag.
!      \item [{[halt]}]
!            Halt definition, with the following valid values:
!            \begin{description}
!              \item {\tt ESMF\_LOG\_HALTWARNING};
!              \item {\tt ESMF\_LOG\_HALTERROR};
!              \item {\tt ESMF\_LOG\_HALTNEVER}.
!            \end{description}
!      \item [{[logtype]}]
!            Defines either single or multilog.
!      \item [{[stream]}]
!            The type of stream, with the following valid values and meanings:
!            \begin{description}
!              \item 0 \  free;
!              \item 1 \  preordered.
!            \end{description}
!      \item [{[maxElements]}]
!            Maximum number of elements in the Log.
!      \item [{[rc]}]
!            Return code; equals {\tt ESMF\_SUCCESS} if there are no errors.
!      \end{description}
! 
!EOPI

        type(ESMF_LOG),pointer          :: alog

        if (present(log)) then
          alog => log
        else
          alog => ESMF_LogDefault
        endif

	if (present(rc)) then
          rc=ESMF_FAILURE
        endif

	if (present(verbose)) then
          verbose=alog%verbose
        endif
	if (present(flush)) then
          flush=alog%flushImmediately
        endif
	if (present(rootOnly)) then
          rootOnly=alog%rootOnly
        endif
	if (present(halt)) then
          halt=alog%halt
        endif
	if (present(logtype)) then
          logtype=alog%logtype
        endif
	if (present(stream)) then
          stream=alog%stream
        endif
	if (present(maxElements)) then
          maxElements=alog%maxElements	
        endif

	if (present(rc)) then
          rc=ESMF_SUCCESS
        endif

end subroutine ESMF_LogGet

!--------------------------------------------------------------------------
!BOPI
! !IROUTINE: ESMF_LogInitialize - Initialize Log file(s)

! !INTERFACE: 
      subroutine ESMF_LogInitialize(filename, logtype, rc)
!
! !ARGUMENTS:
      character(len=*)                                  :: filename
      type(ESMF_LogType), intent(in),optional           :: logtype  
      integer, intent(out),optional	                :: rc

! !DESCRIPTION:
!      This routine initializes the global default {\tt ESMF\_Log}.  
!      The default {\tt ESMF\_Log} is assigned the {\tt filename} and 
!      is associated with an open Fortran unit number.
!
!      The arguments are:
!      \begin{description}
! 
!      \item [filename]
!            Name of file.  Maximum length 26 characters to allow for
!            the PET number to be added and keep the total file name
!            length under 32 characters.
!      \item [{[logtype]}]
!            Specifies {\tt ESMF\_LOG\_SINGLE}, {\tt ESMF\_LOG\_MULTI} or
!            {\tt ESMF\_LOG\_NONE}.
!      \item [{[rc]}]
!            Return code; equals {\tt ESMF\_SUCCESS} if there are no errors.
!      \end{description}
! 
!EOPI
	
    call ESMF_LogOpen(ESMF_LogDefault, filename, logtype, rc)

end subroutine ESMF_LogInitialize

!--------------------------------------------------------------------------
!BOP
! !IROUTINE: ESMF_LogMsgFoundAllocError - Check Fortran status for allocation 
!            error and write message

! !INTERFACE: 
	function ESMF_LogMsgFoundAllocError(statusToCheck,msg,line,file, &
                                            method,rcToReturn,log)
!
! !RETURN VALUE:
	logical                                     ::ESMF_LogMsgFoundAllocError
! !ARGUMENTS:
!	
	integer, intent(in)                         :: statusToCheck
	character(len=*), intent(in)                :: msg
	integer, intent(in), optional               :: line
	character(len=*), intent(in), optional      :: file
	character(len=*), intent(in), optional	    :: method
        integer, intent(out),optional               :: rcToReturn	
        type(ESMF_LOG), intent(in), optional	    :: log

! !DESCRIPTION:
!      This function returns a logical true when a Fortran status code
!      returned from a memory allocation indicates an allocation error.
!      An ESMF predefined memory allocation error message 
!      will be added to the {\tt ESMF\_Log} along with a user added {\tt msg}, 
!      {\tt line}, {\tt file} and 
!      {\tt method}.  Additionally, statusToCheck will be converted to 
!      {\tt rcToReturn}.
!
!      The arguments are:
!      \begin{description}
! 	
!      \item [statusToCheck]
!            Fortran allocation status to check.
!      \item [msg]
!            User-provided message string.
!      \item [{[line]}]
!            Integer source line number.  Expected to be set by
!            using the preprocessor macro {\tt \_\_LINE\_\_} macro.
!      \item [{[file]}]
!            User-provided source file name. 
!      \item [{[method]}]
!            User-provided method string.
!      \item [{[rcToReturn]}]
!            If specified, set the {\tt rcToReturn} value to 
!            {\tt ESMF\_RC\_MEM} which is the error code for a memory 
!            allocation eror.
!      \item [{[log]}]
!            An optional {\tt ESMF\_Log} object that can be used instead
!	     of the default Log.
!      
!      \end{description}
! 
!EOP
    character(len=ESMF_MAXSTR)::tempmsg
    character(len=ESMF_MAXSTR)::allocmsg
    integer::msglen=0
    
    ESMF_LogMsgFoundAllocError=.FALSE.
    if (statusToCheck .NE. 0) then
        call c_esmc_loggeterrormsg(ESMF_RC_MEM,tempmsg,msglen)
	if (present(rcToReturn)) then
            rcToReturn=ESMF_RC_MEM
        endif
        allocmsg=tempmsg(1:msglen)
	call ESMF_LogWrite(trim(allocmsg)//msg,ESMF_LOG_ERROR,line,file,method,log)	
	ESMF_LogMsgFoundAllocError=.TRUE.
    else
        if (present(rcToReturn)) then
            rcToReturn=ESMF_SUCCESS
        endif
    endif	
       
end function ESMF_LogMsgFoundAllocError

!--------------------------------------------------------------------------
!BOP
! !IROUTINE: ESMF_LogMsgFoundError - Check ESMF return code for error and write message

! !INTERFACE: 
	function ESMF_LogMsgFoundError(rcToCheck, msg, line, file, method, &
                                       rcToReturn, log)
!
! !RETURN VALUE:
	logical                                         ::ESMF_LogMsgFoundError
! !ARGUMENTS:
!	
	integer, intent(in)                             :: rcToCheck
	character(len=*), intent(in)                    :: msg
	integer, intent(in), optional                   :: line
	character(len=*), intent(in), optional          :: file
	character(len=*), intent(in), optional	        :: method
	integer, intent(out),optional                   :: rcToReturn
	type(ESMF_LOG), intent(in), optional		:: log
	

! !DESCRIPTION:
!      This function returns a logical true for ESMF return codes that indicate
!      an error.  A predefined error message will added to the {\tt ESMF\_Log} 
!      along with
!      a user added {\tt msg}, {\tt line}, {\tt file} and {\tt method}.  
!      Additionally, {\tt rcToReturn} will be set to {\tt rcToCheck}.
!
!      The arguments are:
!      \begin{description}
! 	
!      \item [rcToCheck]
!            Return code to check.
!      \item [msg]
!            User-provided message string.
!      \item [{[line]}]
!            Integer source line number.  Expected to be set by
!            using the preprocessor macro {\tt \_\_LINE\_\_} macro.
!      \item [{[file]}]
!            User-provided source file name. 
!      \item [{[method]}]
!            User-provided method string.
!      \item [{[rcToReturn]}]
!            If specified, copy the {\tt rcToCheck} value to {\tt rc}.
!            This is not the return code for this function; it allows
!            the calling code to do an assignment of the error code
!            at the same time it is testing the value.
!      \item [{[log]}]
!            An optional {\tt ESMF\_Log} object that can be used instead
!            of the default Log.
!      
!      \end{description}
! 
!EOP
	
    ESMF_LogMsgFoundError=.FALSE.
	if (present(rcToReturn)) then
            rcToReturn = rcToCheck
        endif
	if (rcToCheck .NE. ESMF_SUCCESS) then
	    call ESMF_LogWrite(msg,ESMF_LOG_ERROR,line,file,method,log)
	    ESMF_LogMsgFoundError=.TRUE.
	endif	
       
end function ESMF_LogMsgFoundError

!--------------------------------------------------------------------------
!BOP
! !IROUTINE: ESMF_LogMsgSetError - Set ESMF return code for error and write msg

! !INTERFACE: 
	subroutine ESMF_LogMsgSetError(rcValue, msg, line, file, method, &
                                       rcToReturn, log)

! !ARGUMENTS:
!	
	integer, intent(in)                             :: rcValue
	character(len=*), intent(in)                    :: msg
	integer, intent(in), optional                   :: line
	character(len=*), intent(in), optional          :: file
	character(len=*), intent(in), optional	        :: method
	integer, intent(out),optional                   :: rcToReturn
	type(ESMF_LOG), intent(in), optional		:: log
	

! !DESCRIPTION:
!      This subroutine sets the {\tt rcToReturn} value to {\tt rcValue} if
!      {\tt rcToReturn} is present and writes this error code to the {\tt ESMF\_Log}
!      if an error is generated.  A predefined error message will added to the 
!      {\tt ESMF\_Log} along with a user added {\tt msg}, {\tt line}, {\tt file}
!      and {\tt method}.  
!
!      The arguments are:
!      \begin{description}
! 	
!      \item [rcValue]
!            rc value for set
!      \item [msg]
!            User-provided message string.
!      \item [{[line]}]
!            Integer source line number.  Expected to be set by
!            using the preprocessor macro {\tt \_\_LINE\_\_} macro.
!      \item [{[file]}]
!            User-provided source file name. 
!      \item [{[method]}]
!            User-provided method string.
!      \item [{[rcToReturn]}]
!            If specified, copy the {\tt rcValue} value to {\tt rcToreturn}.
!            This is not the return code for this function; it allows
!            the calling code to do an assignment of the error code
!            at the same time it is testing the value.
!      \item [{[log]}]
!            An optional {\tt ESMF\_Log} object that can be used instead
!	     of the default Log.
!      
!      \end{description}
! 
!EOP
	
    if (present(rcToReturn)) then
        rcToReturn = rcValue
    endif
    if (rcValue .NE. ESMF_SUCCESS) then
        call ESMF_LogWrite(msg,ESMF_LOG_ERROR,line,file,method,log)
    endif	
       
end subroutine ESMF_LogMsgSetError

!--------------------------------------------------------------------------
!BOP
! !IROUTINE: ESMF_LogOpen - Open Log file(s)

! !INTERFACE: 
    subroutine ESMF_LogOpen(log, filename, logtype, rc)
!
! !ARGUMENTS:
    type(ESMF_Log)			                :: log
    character(len=*)			                :: filename
    type(ESMF_LogType), intent(in),optional             :: logtype  
    integer, intent(out),optional	                :: rc

! !DESCRIPTION:
!      This routine opens a file with {\tt filename} and associates
!      it with the {\tt ESMF\_Log}.  This is only
!      used when the user does not want to use the default Log.
!
!      The arguments are:
!      \begin{description}
! 
!      \item [log]
!            An {\tt ESMF\_Log} object.
!      \item [filename]
!            Name of file.  Maximum length 26 characters to allow for
!            the PET number to be added and keep the total file name
!            length under 32 characters.
!      \item [{[logtype]}]
!            Set the logtype. See section \ref{opt:logtype} for a list of 
!            valid options.
!            If not specified, defaults to {\tt ESMF\_LOG\_MULTI}.
!      \item [{[rc]}]
!            Return code; equals {\tt ESMF\_SUCCESS} if there are no errors.
!      \end{description}
! 
!EOP
    interface 
      subroutine f_ESMF_VMGlobalGet(localPet, petCount)
        integer, intent(out), optional  :: localPet
        integer, intent(out), optional  :: petCount
      end subroutine f_ESMF_VMGlobalGet
    end interface

    integer 				                   :: status, i, j, rc2
    type(ESMF_LOGENTRY), dimension(:), pointer             :: localbuf
    character(len=32)                                      :: fname
    character(len=4)                                       :: fnum
    character(ESMF_MAXSTR)                                 :: petNumChar
    logical                                                :: inuse

    if (present(rc)) then
        rc=ESMF_FAILURE
    endif

    if (log%FileIsOpen .eq. ESMF_TRUE) then 
        print *, "This ESMF_Log is already open with file '", &
                 trim(log%nameLogErrFile), "'"
        return
    endif

    log%maxElements = 10
    log%stream = 0
    log%fIndex = 1

    call f_ESMF_VMGlobalGet(log%petNumber)
    ! Convert PET to contiguous character label
    write(petNumChar, *) log%petNumber
    log%petNumLabel = "PET" // trim(adjustl(petNumChar))

    log%stopprogram = .false.
    log%flushImmediately = ESMF_FALSE
    log%rootOnly = ESMF_FALSE
    log%verbose = ESMF_FALSE
    log%flushed = ESMF_FALSE
    log%dirty = ESMF_FALSE
    log%FileIsOpen=ESMF_FALSE
    log%halt=ESMF_LOG_HALTNEVER
    if (present(logtype)) then
      	log%logtype=logtype
    else
        log%logtype=ESMF_LOG_MULTI
    endif
    
  if(log%logtype .ne. ESMF_LOG_NONE) then
    	
    if (log%logtype .eq. ESMF_LOG_SINGLE) then
        log%nameLogErrFile=trim(filename)
    else
        if (log%petNumber .le. 9) then
	    write(fnum,11) log%petNumber 
        else if (log%petNumber .le. 99) then
            write(fnum,21) log%petNumber
        else if (log%petNumber .le. 9999) then
            write(fnum,31) log%petNumber
        else if (log%petNumber .le. 99999) then
            write(fnum,41) log%petNumber
        else if (log%petNumber .le. 999999) then
            write(fnum,51) log%petNumber
        else 
            write(fnum,*) log%petNumber
        endif
        fname = "PET" // trim(fnum) // "." // trim(filename)
        log%nameLogErrFile=fname
    endif
 11     format(I1.1)
 21     format(I2.2)
 31     format(I3.3)
 41     format(I4.4)
 51     format(I5.5)
    if (len(log%nameLogErrFile) .gt. 32) then
        print *, "Filename exceeded 32 characters."
        if (present(rc)) then
            rc = ESMF_FAILURE
        endif
        return
    endif

    ! find an available unit number
    do j=ESMF_LOG_FORT_STDOUT, ESMF_LOG_UPPER
        inquire(unit=j, opened=inuse, iostat=status)
        if (.not. inuse .and. status .eq. 0) then
	  log%unitNumber= j
          exit
     	endif
    enddo

    ! if no available unit number then error out
    if (status .ne. 0) then
        if (present(rc)) then
            rc=ESMF_FAILURE
        endif
        return
    endif

    ! open the file, with retries
    do i=1, ESMF_LOG_MAXTRYOPEN
        OPEN(UNIT=log%unitNumber,File=log%nameLogErrFile,& 
	     POSITION="APPEND", ACTION="WRITE", STATUS="UNKNOWN", IOSTAT=status)
        if (status.eq.0) then
            log%FileIsOpen = ESMF_TRUE
            exit
        endif
    enddo

    ! if unable to open file then error out
    if (log%FileIsOpen .ne. ESMF_TRUE) then
        if (present(rc)) then
            rc=ESMF_FAILURE
        endif
        return
    endif

    ! BEWARE:  absoft 8.0 compiler bug - if you try to allocate directly
    ! you get an error.  if you allocate a local buffer and then point the
    ! derived type buffer at it, it works.  go figure.
    
    allocate(localbuf(log%maxElements), stat=status)
    if (status .ne. 0) then
      print *, "Allocation of buffer failed."
      if (present(rc)) then
          rc = ESMF_FAILURE
      endif
      return
    endif
    log%LOG_ENTRY => localbuf
    
  endif
  
    call c_ESMC_LogInitialize(filename,log%petNumber,log%logtype,rc2)
    if (present(rc)) then
        rc=ESMF_SUCCESS    
    endif
    
end subroutine ESMF_LogOpen	

!--------------------------------------------------------------------------
!BOP
! !IROUTINE: ESMF_LogSet - Set Log parameters

! !INTERFACE: 
	subroutine ESMF_LogSet(log,verbose,flush,rootOnly,halt, &
                               stream,maxElements,rc)
!
! !ARGUMENTS:
!	
	type(ESMF_LOG), target,optional                         :: log
	type(ESMF_Logical), intent(in),optional			:: verbose
	type(ESMF_Logical), intent(in),optional			:: flush
	type(ESMF_Logical), intent(in),optional			:: rootOnly
	type(ESMF_HaltType), intent(in),optional                :: halt
	integer, intent(in),optional			        :: stream  
	integer, intent(in),optional			        :: maxElements
	integer, intent(out),optional			        :: rc
	
! !DESCRIPTION:
!      This subroutine sets the properties for the Log object.
!
!      The arguments are:
!      \begin{description}
!
!      \item [{[log]}]
!            An optional {\tt ESMF\_Log} object that can be used instead
!            of the default Log.
!      \item [{[verbose]}]
!            Verbose flag.
!      \item [{[rootOnly]}]
!	     Root only flag.
!      \item [{[halt]}]
!	     Halt definition, with the following valid values:
!            \begin{description}
!              \item {\tt ESMF\_LOG\_HALTWARNING};
!              \item {\tt ESMF\_LOG\_HALTERROR};
!              \item {\tt ESMF\_LOG\_HALTNEVER}.
!            \end{description}
!      \item [{[stream]}]
!            The type of stream, with the following valid values and meanings:
!            \begin{description}
!              \item 0 \  free;
!              \item 1 \  preordered. 
!            \end{description}
!      \item [{[maxElements]}]
!            Maximum number of elements in the Log.
!      \item [{[rc]}]
!            Return code; equals {\tt ESMF\_SUCCESS} if there are no errors.
!      \end{description}
! 
!EOP
    integer :: i, status
    type(ESMF_LOG), pointer          :: alog
    type(ESMF_LOGENTRY), dimension(:), pointer :: localbuf

    if (present(log)) then
      alog => log
    else
      alog => ESMF_LogDefault
    endif

    if (present(rc)) then
        rc=ESMF_FAILURE
    endif

    if (alog%FileIsOpen .ne. ESMF_TRUE) then
      print *, "ESMF_Log not open -- cannot ESMF_LogSet()."
      return
    endif
    
    if (present(verbose)) then
        alog%verbose=verbose
    endif
    if (present(flush)) then
        alog%flushImmediately=flush
    endif
    if (present(rootOnly)) then
        alog%rootOnly=rootOnly
    endif
    if (present(halt)) then
        alog%halt=halt
    endif
    if (present(stream)) then
        alog%stream=stream
    endif
    if (present(maxElements)) then
      if (maxElements.gt.0 .AND. alog%maxElements.ne.maxElements) then
        allocate(localbuf(maxElements), stat=status)

        ! if the current number of log entries is greater than the new
        ! maxElements, then call flush.  Otherwise copy old contents over.
        if (alog%fIndex.ge.maxElements) then
          call ESMF_LogFlush(alog,rc=status)
        else
          do i = 1,alog%fIndex
            call ESMF_LogEntryCopy(alog%LOG_ENTRY(i), localbuf(i), rc=status)
          enddo
        endif
        deallocate(alog%LOG_ENTRY,stat=status)
        alog%LOG_ENTRY => localbuf
        alog%maxElements=maxElements
      endif
    endif    

    if (present(rc)) then
        rc=ESMF_SUCCESS 
    endif

end subroutine ESMF_LogSet

!--------------------------------------------------------------------------
!BOP
! !IROUTINE: ESMF_LogWrite - Write to Log file(s)

! !INTERFACE: 
	subroutine ESMF_LogWrite(msg,MsgType,line,file,method,log,rc)
!
!
! !ARGUMENTS:
	character(len=*), intent(in)                :: msg
	type(ESMF_MsgType), intent(in)              :: msgtype
	integer, intent(in), optional               :: line
	character(len=*), intent(in), optional      :: file
	character(len=*), intent(in), optional	    :: method
	type(ESMF_LOG),target,optional   	    :: log
	integer, intent(out),optional		    :: rc

! !DESCRIPTION:
!      This subroutine writes to the file associated with an {\tt ESMF\_Log}.
!      A message is passed in along with the {\tt msgtype}, {\tt line}, 
!      {\tt file} and {\tt method}.  If the write to the {\tt ESMF\_Log}
!      is successful, the function will return a logical {\tt true}.  This 
!      function is the base function used by all the other {\tt ESMF\_Log} 
!      writing methods.
!
!      The arguments are:
!      \begin{description}
! 
!      \item [msg]
!            User-provided message string.
!      \item [msgtype]
!            The type of message.  See Section~\ref{opt:msgtype} for
!            possible values.
!      \item [{[line]}]
!            Integer source line number.  Expected to be set by
!            using the preprocessor macro {\tt \_\_LINE\_\_} macro.
!      \item [{[file]}]
!            User-provided source file name. 
!      \item [{[method]}]
!            User-provided method string.
!      \item [{[log]}]
!            An optional {\tt ESMF\_Log} object that can be used instead
!	     of the default Log.
!      \item [{[rc]}]
!            Return code; equals {\tt ESMF\_SUCCESS} if there are no errors.
!      \end{description}
! 
!EOP
    interface 
      subroutine f_ESMF_VMAbort(rc)
        integer, intent(out), optional :: rc    
      end subroutine f_ESMF_VMAbort
    end interface
    
    character(len=10)               :: t
    character(len=8)                :: d
    !character(len=7)               :: lt
    character(len=32)               ::tmethod,tfile
    integer			    ::tline
    integer                         ::h,m,s,ms,y,mn,dy
    integer			    ::rc2,index
    type(ESMF_LOG),pointer          :: alog
    
    if (present(log)) then
      alog => log
    else
      alog => ESMF_LogDefault
    endif

    if (alog%logtype .ne. ESMF_LOG_NONE) then

        if (alog%FileIsOpen .ne. ESMF_TRUE) then
          print *, "ESMF_Log not open -- cannot ESMF_LogWrite()."
          rc=ESMF_FAILURE
          return
        endif

        index = alog%fIndex
      
    	alog%dirty = ESMF_TRUE    
    	call c_esmc_timestamp(y,mn,dy,h,m,s,ms)
    	call DATE_AND_TIME(d,t)	
    	if (present(rc)) then
            rc=ESMF_FAILURE
        endif
    	alog%LOG_ENTRY(index)%methodflag = .FALSE.
    	alog%LOG_ENTRY(index)%lineflag = .FALSE.
    	alog%LOG_ENTRY(index)%fileflag = .FALSE.
    	if (present(method)) then
        	tmethod=adjustl(method)
		alog%LOG_ENTRY(index)%methodflag=.TRUE.
		alog%LOG_ENTRY(index)%method = tmethod
    	endif	
    	if (present(line)) then
        	tline=line 
		alog%LOG_ENTRY(index)%lineflag = .TRUE.
		alog%LOG_ENTRY(index)%line = tline
    	endif	
    	if (present(file)) then
        	tfile=adjustl(file)
		alog%LOG_ENTRY(index)%fileflag = .TRUE.
		alog%LOG_ENTRY(index)%file = tfile
    	endif
    	select case (msgtype%mtype)
        case (1)
    	    alog%LOG_ENTRY(index)%lt="INFO"
        case (2)
    	    alog%LOG_ENTRY(index)%lt="WARNING"
   	case default
    	    alog%LOG_ENTRY(index)%lt="ERROR"
    	end select	
    	alog%LOG_ENTRY(alog%fIndex)%d = d
    	alog%LOG_ENTRY(alog%fIndex)%h = h
    	alog%LOG_ENTRY(alog%fIndex)%m = m
    	alog%LOG_ENTRY(alog%fIndex)%s = s
    	alog%LOG_ENTRY(alog%fIndex)%ms = ms	
    	alog%LOG_ENTRY(alog%fIndex)%msg = msg
	alog%flushed = ESMF_FALSE	
    	if ((ESMF_LogDefault%halt .eq. ESMF_LOG_HALTERROR).and. (msgtype .eq. ESMF_LOG_ERROR)) then
        	alog%stopprogram=.TRUE.
        	call ESMF_LogClose(alog,rc=rc2)
    	endif    	 
    	if ((alog%halt .eq. ESMF_LOG_HALTWARNING).and. (msgtype .gt. ESMF_LOG_WARNING)) then
        	alog%stopprogram=.TRUE.
        	call ESMF_LogClose(alog,rc=rc2)
    	endif
    	if (alog%fIndex .eq. alog%maxElements .or. &
            alog%flushImmediately .eq. ESMF_TRUE) then
	        alog%fIndex = alog%fIndex + 1	
        	call ESMF_LogFlush(alog,rc=rc2) 
		alog%fIndex = 1
    	else
        	alog%fIndex = alog%fIndex + 1	
    	endif	
    endif
    ! if requested, halt the program right now.
    if (alog%stopprogram) call f_ESMF_VMAbort()
    if (present(rc)) then
        rc=ESMF_SUCCESS
    endif
end subroutine ESMF_LogWrite

!--------------------------------------------------------------------------
!BOPI
! !IROUTINE: ESMF_LogEntryCopy - Copy a Log entry

! !INTERFACE: 
	subroutine ESMF_LogEntryCopy(logEntryIn, logEntryOut, rc)
!
! !ARGUMENTS:
        type(ESMF_LOGENTRY), intent(in)  :: logEntryIn
        type(ESMF_LOGENTRY), intent(out) :: logEntryOut
        integer, intent(out), optional   :: rc

! !DESCRIPTION:
!      This routine copies the internals from one log entry to another.
!
!      The arguments are:
!      \begin{description}
! 
!      \item [logEntryIn]
!            Log entry to copy from.
!      \item [logEntryOut]
!            Log entry to copy into.
!      \item [{[rc]}]
!            Return code; equals {\tt ESMF\_SUCCESS} if there are no errors.
!      \end{description}
! 
!EOPI
    
    logEntryOut%h    = logEntryIn%h
    logEntryOut%m    = logEntryIn%m
    logEntryOut%s    = logEntryIn%s
    logEntryOut%ms   = logEntryIn%ms
    logEntryOut%line = logEntryIn%line

    logEntryOut%methodflag = logEntryIn%methodflag
    logEntryOut%lineflag   = logEntryIn%lineflag
    logEntryOut%fileflag   = logEntryIn%fileflag

    logEntryOut%msg    = logEntryIn%msg
    logEntryOut%file   = logEntryIn%file
    logEntryOut%method = logEntryIn%method
    logEntryOut%d      = logEntryIn%d
    logEntryOut%lt     = logEntryIn%lt
    
    if (present(rc)) then
        rc=ESMF_SUCCESS
    endif

end subroutine ESMF_LogEntryCopy

end module ESMF_LogErrMod

